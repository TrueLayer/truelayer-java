plugins {
    id 'java-library'
    // to unleash the lombok magic
    id "io.freefair.lombok" version "6.2.0"
    // to support Kotlin
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    // to make our tests output more fancy
    id 'com.adarshr.test-logger' version '3.1.0'
    // to build fat jars
    id "com.github.johnrengelman.shadow" version "7.1.0"
    // to publish packages
    id 'maven-publish'
}

version = '0.2.2-pre3'
archivesBaseName = 'truelayer-java'

shadowJar {
    archiveBaseName.set(archivesBaseName)
    archiveClassifier.set('')
    archiveVersion.set(version)
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

test {
    useJUnitPlatform()
}

testlogger {
    theme 'mocha'
}

tasks.register('unit-tests', Test) {
    outputs.upToDateWhen { false }
    useJUnitPlatform{
        excludeTags "integration"
    }
}

tasks.register('integration-tests', Test) {
    outputs.upToDateWhen { false }
    useJUnitPlatform{
        includeTags "integration"
    }
}

dependencies {
    // Use JUnit test framework.
    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // https://mvnrepository.com/artifact/com.nimbusds/nimbus-jose-jwt
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.15.2'

    //Needed to parse EC pem files
    implementation("org.bouncycastle:bcprov-jdk15on:1.69")
    implementation("org.bouncycastle:bcpkix-jdk15on:1.69")

    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

    implementation group: 'org.apache.commons', name: 'commons-configuration2', version: '2.7'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'


    //Jws library support
    implementation("com.nimbusds:nimbus-jose-jwt:9.15.2")

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.0'


    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.0.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.0.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.0.0'

    testImplementation group: "com.github.tomakehurst", name:"wiremock-jre8", version: "2.31.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/TrueLayer/truelayer-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}